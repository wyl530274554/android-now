plugins {
    id 'com.android.application'
}

//Git提交次数
private static def getGitCommitCount() {
    return "git rev-list --count HEAD".execute().getText().toInteger()
}

//Git提交唯一标识id，如a24e796d
private static def getGitCommitId() {
    return "git rev-parse --short HEAD".execute().getText().toString().trim()
}

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    //主版本号
    int major = 1
    //子版本号
    int minor = 0

    defaultConfig {
        applicationId "com.melon.unity"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode getGitCommitCount()
        versionName major + "." + minor + "." + getGitCommitId()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        common {
            keyAlias 'melon'
            keyPassword '123456'
            storeFile file('myapp.jks')
            storePassword '123456'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.common
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding = true
    }

    //包名
    applicationVariants.all {
        variant ->
            variant.outputs.all {
                outputFileName = "melon_" + defaultConfig.versionName + "_" + defaultConfig.versionCode + ".apk"
            }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment:2.3.1'
    implementation 'androidx.navigation:navigation-ui:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation project(path: ':commonlib')
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'androidx.preference:preference:1.1.1'
    testImplementation 'junit:junit:4.13.2'
}